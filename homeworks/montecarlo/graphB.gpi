set terminal svg font "Times New Roman, 14" background "black"
W = "tc 'white'"


FILE = "comparison.data"


set multiplot layout 2,1 rowsfirst
# x- and ytics for each row resp. column
NOXTICS = "set xtics ('' 1,'' 2,'' 3,'' 4,'' 5, '' 6, '' 7, '' 8, '' 9, '' 10) tc 'white'"
XTICS = "set xtics ('10' 1, '' 2, '1000' 3, '' 4, '100000' 5, '' 6, '10000000' 7, '' 8, '1000000000' 9, '' 10) tc 'white'"

#Position on screen
TMARGIN = "set tmargin at screen 0.90; set bmargin at screen 0.56"
BMARGIN = "set tmargin at screen 0.54; set bmargin at screen 0.20"
HMARGIN = "set lmargin at screen 0.15; set rmargin at screen 0.95"


set key bottom right tc 'white'
set border lc 'white'

##############################################################################

h(x) = 4*pi
f(x) = A*1.0/sqrt(x)

fit f(x) FILE using ($1):($3) via A


@NOXTICS; @TMARGIN; @HMARGIN
set ylabel "Evaluation of integral" @W 
plot [0:10][0:5*pi]\
	FILE using (log10($1)):($2) with points pt 7 ps 0.6 lc rgb "gold" \
	title "plain mc", \
	FILE using (log10($1)):($4) with points pt 7 ps 0.5 lc rgb "orchid" \
	title "quasi mc", \
	h(x) title "exact result"

set key top right tc 'white'

@XTICS; @BMARGIN; @HMARGIN
set xlabel "Number of points, N" tc 'white'
set ylabel "Error estimate" tc 'white' offset 1,0,0
plot [0:10][-0.05:0.65]\
	FILE using (log10($1)):($3) with points pt 7 ps 0.6 lc rgb "gold" \
	title "plain mc error", \
	FILE using (log10($1)):($5) with points pt 7 ps 0.5 lc rgb "orchid" \
	title "quasi mc error", \
	f(10**x) title "fit"
	
