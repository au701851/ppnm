set terminal svg font "Times New Roman, 14" background "black"
W = "tc 'white'"
set fit quiet

FILE = "comparison.data"


set multiplot layout 2,1 rowsfirst
# x- and ytics for each row resp. column
NOXTICS = "set xtics ('' 2000, '' 4000, '' 6000, '' 8000, '' 10000) tc 'white'"
XTICS = "set xtics ('2000' 2000, '4000' 4000, '6000' 6000, '8000' 8000, '10000' 10000) tc 'white'"

#Position on screen
TMARGIN = "set tmargin at screen 0.90; set bmargin at screen 0.56"
BMARGIN = "set tmargin at screen 0.54; set bmargin at screen 0.20"
HMARGIN = "set lmargin at screen 0.15; set rmargin at screen 0.95"


set key bottom right tc 'white'
set border lc 'white'

##############################################################################

h(x) = 4*pi
f(x) = A*1.0/sqrt(x)+B

fit f(x) FILE using ($1):($3) via A, B


@NOXTICS; @TMARGIN; @HMARGIN
set ylabel "Evaluation of integral" @W 
plot [0:10000][8:5*pi]\
	FILE using ($1):($2) with points pt 7 ps 0.6 lc rgb "gold" \
	title "plain mc", \
	FILE using ($1):($4) with points pt 7 ps 0.5 lc rgb "orchid" \
	title "quasi mc", \
	h(x) linewidth 2 title "exact result" 

set key top right tc 'white'

@XTICS; @BMARGIN; @HMARGIN
set xlabel "Number of points, N" tc 'white'
set ylabel "Error estimate" tc 'white' offset 1,0,0
plot [0:10000][-0.05:0.65]\
	FILE using ($1):($3) with points pt 7 ps 0.6 lc rgb "gold" \
	title "plain mc error (estimated)", \
	FILE using ($1):($5) with points pt 7 ps 0.5 lc rgb "orchid" \
	title "quasi mc error (estimated)", \
	f(x) linewidth 2 title "fit of plain mc-error to 1/sqrt(N)" 
	
