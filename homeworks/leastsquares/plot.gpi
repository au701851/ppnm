set terminal svg font "Times New Roman, 18"
set output "Plot.svg"

set xlabel "Time (days)"
set ylabel "Activity of ThX (relative units)"

set key right

set style fill transparent solid 0.2 noborder
a_err = 0.0339477544244211
lambda_err = 0.00695008119420513
a = 4.9511553496486
lambda = -0.169887082001008


f(x) = exp(a)*exp(lambda*x)
fup(x) = exp(a+a_err)*exp((lambda+lambda_err)*x)
flo(x) = exp(a-a_err)*exp((lambda-lambda_err)*x)



plot [0:15][0.0:120.0]\
	'+' using 1:(flo($1)):(fup($1)) with filledcurves closed \
	title "Fit with error on parameters", \
	"rad_decay_formatted.txt" using ($1):($2):($4) with errorbars \
	title "Data from Rutherford and Soddy",\
	f(x) with lines dt 2 title "Best fit"

