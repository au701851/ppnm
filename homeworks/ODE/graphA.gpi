set terminal svg font "Times New Roman, 14"
set output "GraphA.svg"

set fit logfile "fit.log"
set fit quiet
#Setting fit quiet stops it from printing its log on screen

set multiplot layout 2,1 rowsfirst
# x- and ytics for each row resp. column
NOXTICS = "set xtics ('' -2*pi,'' -pi,'' 0,'' pi,'' 2*pi); \
          unset xlabel"
XTICS = "set xtics ('-2π' -2*pi,'-π' -pi,'0' 0,'π' pi,'2π' 2*pi);\
          set xlabel 'x'"
NOYTICS = "set format y ''; unset ylabel"
YTICS = "set format y '%.0f'; set ylabel 'y'"

#Position on screen
TMARGIN = "set tmargin at screen 0.90; set bmargin at screen 0.55"
BMARGIN = "set tmargin at screen 0.55; set bmargin at screen 0.20"
HMARGIN = "set lmargin at screen 0.15; set rmargin at screen 0.95"


set macros
# Placement of the a,b,c,d labels in the graphs
POS = "at graph 0.20,0.9 font ',18'"


set key top right

##############################################################################



f(x) = a*sin(x)
fit f(x) "diffeq1.data" using ($1):($2) via a

g(x) = A*exp(-k*x)
fit g(x) "diffeq2.data" using ($1):($2) via A, k

set label 2 "U'' = -U" @POS
@NOXTICS; @YTICS; @TMARGIN; @HMARGIN
plot [0:2*pi][-1.2:2]\
	"diffeq1.data" using ($1):($2) with points pt 7 ps 0.3 lc rgb "red" \
	title "Solution for y_1 = U", \
	f(x) title "Fit of y_1 to sine (expected result)", \
	"diffeq1.data" using ($1):($3) with points pt 7 ps 0.3 lc rgb "green" \
	title "Solution for y_2 = U'"


set label 2 "U'' = U" @POS
@XTICS; @YTICS; @BMARGIN; @HMARGIN
plot [0:2*pi][0:1.1]\
	"diffeq2.data"using ($1):($2) with points pt 7 ps 0.3 lc rgb "red" \
	title "Solution for y_1 = U", \
	g(x) title "Fit to exponential"
