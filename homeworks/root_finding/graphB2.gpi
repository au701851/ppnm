set terminal svg font "Times New Roman, 12" background "black"
set term svg dynamic size 900, 350

set margins 2,1,2,1

FILE = "convergence.data"

W = 'tc "white"'

set multiplot layout 1,3 rowsfirst



set xtics @W
set ytics @W
NOYTICS = "unset ytics; unset ylabel"


set border lc 'white'

f1(x) = -0.5

#######################################################

set key top right @W

set tmargin at screen 0.95; set bmargin at screen 0.15

set lmargin at screen 0.05; set rmargin at screen 0.35
set xlabel "rmax" @W 
set ylabel "E_0" @W offset 3,0,0
plot [-1:11][-0.7:0]\
	FILE using ($1):($2) with points pt 7 ps 0.5 lc rgb "gold" \
	title "calculated",\
	f1(x) with lines dt 1 lw 3 lc rgb "orchid"\
	title "expected"

set lmargin at screen 0.35; set rmargin at screen 0.65
@NOYTICS
set xlabel "rmin" @W 
plot [0.01:0.65][-0.7:0]\
	FILE using ($3):($4) with points pt 7 ps 0.5 lc rgb "gold" \
	title "calculated",\
	f1(x) with lines dt 1 lw 3 lc rgb "orchid"\
	title "expected"


set lmargin at screen 0.65; set rmargin at screen 0.95
set xlabel "accuracy of ODE-solver" @W 
plot [0:10][-0.7:0]\
	FILE using ($5):($6) with points pt 7 ps 0.5 lc rgb "gold" \
	title "calculated",\
	f1(x) with lines dt 1 lw 3  lc rgb "orchid"\
	title "expected"
